plugins {
    id "fr.brouillard.oss.gradle.jgitver" version "+"
}
group = 'net.byteflux'

apply from: 'https://raw.githubusercontent.com/code-lime/org.lime.core/refs/tags/3.1.1/version.gradle'

println "Project version: $version"

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.compilerArgs.add('-parameters')
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from(components["java"])
                groupId = 'net.byteflux'
                artifactId = "libby-${project.name}"
            }
        }
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/${System.getenv("GITHUB_REPOSITORY")}")
                credentials {
                    username = System.getenv("GITHUB_OWNER")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        }
    }
}
